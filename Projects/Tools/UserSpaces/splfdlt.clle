
/*--------------------------------------------------------------------------*/
/*                                                                          */ 
/* Delete a selection of spool files                                        */
/*                                                                          */
/* Dates: 2025/08/05 Creation                                               */
/*                                                                          */
/*--------------------------------------------------------------------------*/

PGM PARM(&USER &ACTION &FORMTYPE &USRDTA)

DCLPRCOPT DFTACTGRP(*NO) ACTGRP(USRSPC)

DCL VAR(&USER) TYPE(*CHAR) LEN(10)
DCL VAR(&ACTION) TYPE(*CHAR) LEN(8)
DCL VAR(&FORMTYPE) TYPE(*CHAR) LEN(10)
DCL VAR(&USRDTA) TYPE(*CHAR) LEN(10)

DCL VAR(&USRSPC) TYPE(*CHAR) LEN(10)
DCL VAR(&USRSPCLIB) TYPE(*CHAR) LEN(10)
DCL VAR(&USRSPCQUAL) TYPE(*CHAR) LEN(20)

DCL VAR(&NOTDELETED) TYPE(*UINT) LEN(4)
DCL VAR(&DELETED) TYPE(*UINT) LEN(4)
DCL VAR(&NOTDELETEA) TYPE(*CHAR) LEN(4)
DCL VAR(&DELETEDA) TYPE(*CHAR) LEN(4)
DCL VAR(&FORBOTH) TYPE(*LGL)

INCLUDE SRCSTMF('../../Common/Includes/inc_variables_declare.clle')
INCLUDE SRCSTMF('../../Common/Includes/inc_stdapi_declare.clle')
INCLUDE SRCSTMF('../../Common/Includes/inc_QUSLSPL_SPLF0300_declare.clle')

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Include error handling standard routine.                                 */
/*                                                                          */
/*--------------------------------------------------------------------------*/

INCLUDE SRCSTMF('../../Common/Includes/inc_errorhandling.clle')

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Some initializations...                                                  */
/*                                                                          */
/* First, we check the number of parameters in case the program was invoked */
/* directly.                                                                */
/* Second, we run the command validity checker in case the program was      */
/* invoked directly. If the checker finds an error, it sends an CPF0006     */
/* *ESCAPE message which will be catched by the error handling routine.     */
/*                                                                          */
/*--------------------------------------------------------------------------*/
 
INCLUDE SRCSTMF('../../Common/includes/inc_variables_init.clle')

IF COND(%PARMS() *NE 4) THEN( +
    SNDPGMMSG TOPGMQ(*SAME) MSGID(CPX6148) MSGF(QCPFMSG) MSGTYPE(*ESCAPE))

CALL PGM(SPLFDLT0) PARM(&USER &ACTION &FORMTYPE &USRDTA)

RTVJOBA NBR(&JOBNBR)
CHGVAR VAR(&USRSPC) VALUE('SPLF' *CAT &JOBNBR)
CHGVAR VAR(&USRSPCLIB) VALUE('QTEMP')

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* &P_xxx variables are used by the standard error handling routine to send */
/* an escape message when there is an error within the routine itself.      */
/*                                                                          */
/*--------------------------------------------------------------------------*/

CHGVAR VAR(&P_MSGID) VALUE('CPDA4A8')
CHGVAR VAR(&P_MSGF) VALUE('QCPFMSG')
CHGVAR VAR(&P_MSGFLIB) VALUE(*LIBL)
CHGVAR VAR(&P_MSGDTA) VALUE('SPLFDLT' *BCAT 'SPLFDLT USRSPC(')
CHGVAR VAR(&P_MSGDTA) VALUE(&P_MSGDTA *TCAT &USRSPCLIB *TCAT '/' *TCAT &USRSPC)
CHGVAR VAR(&P_MSGDTA) VALUE(&P_MSGDTA *TCAT ') USER(' *TCAT &USER)
CHGVAR VAR(&P_MSGDTA) VALUE(&P_MSGDTA *TCAT ') ACTION(' *TCAT &ACTION)
CHGVAR VAR(&P_MSGDTA) VALUE(&P_MSGDTA *TCAT ') FORMTYPE(' *TCAT &FORMTYPE)
CHGVAR VAR(&P_MSGDTA) VALUE(&P_MSGDTA *TCAT ') USRDTA(' *TCAT &USRDTA *TCAT ')')

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Create user space for building list of spool files                       */
/* and populate it with QUSLSPL                                             */
/* In case of error, we stop here                                           */
/*                                                                          */
/*--------------------------------------------------------------------------*/

USRSPCCRT USRSPC(&USRSPCLIB/&USRSPC) ATTRIBUTE(SPLFDLT) TEXT('User space to retain list of spool files')
CHGVAR VAR(&USRSPCQUAL) VALUE(&USRSPC *CAT &USRSPCLIB)
CALL PGM(QUSLSPL) PARM(&USRSPCQUAL   +
                        'SPLF0300'   +
                        &USER        +
                        '*ALL'       +
                        &FORMTYPE    +
                        &USRDTA     +
                        &ERRC0100)
IF COND(&EXCEPTID *NE &BLANK) THEN(DO)
    CHGVAR VAR(&MSGF) VALUE('QCPFMSG')
    SNDPGMMSG TOPGMQ(*PRV) MSGID(&EXCEPTID) MSGDTA(&EXCEPTDTA) MSGF(&MSGF) MSGTYPE(*ESCAPE)
ENDDO

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Retrieve user space list information values.                             */
/* In case of error, we stop here                                           */
/*                                                                          */
/*--------------------------------------------------------------------------*/

USRSPCRTVI USRSPC(QTEMP/&USRSPC)  ERRC0100(&ERRC0100) APIUSED(&APIUSED) FORMATNAME(&FORMATNAME) +
            STARTPOS(&STARTPOS) ENTRYNB(&ENTRYNB) ENTRYLG(&ENTRYLG)      
IF COND(&EXCEPTID *NE &BLANK) THEN(DO)
    CHGVAR VAR(&MSGF) VALUE('QCPFMSG')
    IF (%SST(&EXCEPTID 1 3) *EQ 'USP') THEN(CHGVAR VAR(&MSGF) VALUE(TOOMSGF))
    SNDPGMMSG TOPGMQ(*PRV) MSGID(&EXCEPTID) MSGDTA(&EXCEPTDTA) MSGF(&MSGF) MSGTYPE(*ESCAPE)
ENDDO

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Browse the user space list section                                       */
/* For each entry,                                                          */
/* - if we have an issue,                                                   */
/*   - a diagnostic message is sent                                         */
/*   - we set the "at least one issue indicator" to yes                     */
/*   - we add one to the number of not deleted spool files                  */
/* - if we don't, apply ACTION parameter                                    */
/*   - if ACTION = *LOGONLY                                                 */
/*     - (review subprogram)                                                */
/*   - if ACTION = *DLTONLY                                                 */
/*     - (review subprogram)                                                */
/*   - if action = *BOTH                                                    */
/*     - (review subprogram)                                                */
/*                                                                          */
/*--------------------------------------------------------------------------*/

CHGVAR VAR(&NOTDELETED) VALUE(&ZERO)
CHGVAR VAR(&DELETED) VALUE(&ZERO)
CHGVAR VAR(&ERROR) VALUE(&FALSE)

DOFOR VAR(&I) FROM(1) TO(&ENTRYNB)
    CHGVAR VAR(&CURRPOS) VALUE(&STARTPOS + (&I-1) * &ENTRYLG)
    USRSPCRTVE USRSPC(QTEMP/&USRSPC) STARTPOS(&CURRPOS) ENTRYLG(&ENTRYLG) +
                ERRC0100(&ERRC0100) ENTRYDTA(&ENTRYDTA)

    IF COND(&EXCEPTID *NE &BLANK) THEN(DO)
        CHGVAR VAR(&MSGF) VALUE('QCPFMSG')
        IF (%SST(&EXCEPTID 1 3) *EQ 'USP') THEN(CHGVAR VAR(&MSGF) VALUE(TOOMSGF))
        SNDPGMMSG TOPGMQ(*SAME) MSGID(&EXCEPTID) MSGDTA(&EXCEPTDTA) MSGF(&MSGF) MSGTYPE(*DIAG)
        CHGVAR VAR(&ERROR) VALUE(&TRUE)
        CHGVAR VAR(&NOTDELETED) VALUE(&NOTDELETED + 1)
    ENDDO
    ELSE CMD(DO)
            CHGVAR VAR(&SPLF0300) VALUE(&ENTRYDTA)
            CHGVAR VAR(&SPSPLNBR) VALUE(%BIN(&SPSPLNBRA))
            CHGVAR VAR(&MSGDTA) VALUE(&SPSPLNAME *CAT &SPSPLNBRA *CAT &SPJOBNBR *CAT &SPJOBUSER *CAT &SPJOBNAME)
            SELECT
                WHEN COND(&ACTION *EQ '*LOGONLY') THEN(CALLSUBR SUBR(LOGONLY))
                WHEN COND(&ACTION *EQ '*DLTONLY') THEN(CALLSUBR SUBR(DLTONLY))
                WHEN COND(&ACTION *EQ '*BOTH') THEN(CALLSUBR SUBR(BOTH))
            ENDSELECT       
    ENDDO

ENDDO

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Send final message                                                       */
/* if there is no error, completion message                                 */
/*   if ACTION = *LOGONLY USP0303                                           */
/*   else USP0304                                                           */
/* if there is at least one error USP0311 escape message                    */
/*                                                                          */
/*--------------------------------------------------------------------------*/

CHGVAR VAR(%BIN(&DELETEDA)) VALUE(&DELETED)
CHGVAR VAR(&MSGDTA) VALUE(&DELETEDA)
IF COND(&ERROR) THEN(DO)
    CHGVAR VAR(%BIN(&NOTDELETEA)) VALUE(&NOTDELETED)
    CHGVAR VAR(&MSGDTA) VALUE(&MSGDTA *TCAT &NOTDELETEA)
    CHGVAR VAR(&MSGID) VALUE('USP0311')
    CHGVAR VAR(&MSGTYPE) VALUE('*ESCAPE')
ENDDO
ELSE CMD(DO)
    SELECT
        WHEN COND(&ACTION *EQ '*LOGONLY') THEN(CHGVAR VAR(&MSGID) VALUE('USP0303'))
        OTHERWISE CMD(CHGVAR VAR(&MSGID) VALUE('USP0304'))
    ENDSELECT
    CHGVAR VAR(&MSGTYPE) VALUE('*COMP')
ENDDO
SNDPGMMSG TOPGMQ(*PRV) MSGID(&MSGID) MSGDTA(&MSGDTA) MSGF(TOOMSGF) MSGTYPE(&MSGTYPE)

/*--------------------------------------------------------------------------*/
/* ACTION = *LOGONLY                                                        */
/* We add one to the number of deleted spool files                          */
/* We send USP0301 information message to the joblog                        */
/*                                                                          */
/*--------------------------------------------------------------------------*/

SUBR SUBR(LOGONLY)

    CHGVAR VAR(&DELETED) VALUE(&DELETED + 1)
    SNDPGMMSG TOPGMQ(*SAME) MSGID(USP0301) MSGDTA(&MSGDTA) MSGF(TOOMSGF) MSGTYPE(*INFO)

ENDSUBR

/*--------------------------------------------------------------------------*/
/* ACTION = *DLTONLY                                                        */
/* We try to delete the spool file                                          */
/* If success                                                               */
/*   - we add one to the number of deleted spool files                      */
/* If failure                                                               */
/*   - we set the "at least one issue indicator" to yes                     */
/*   - we add one to the number of not deleted spool files                  */
/*   - we send USP0312 diag message to help diagnosis                       */
/*                                                                          */
/* This subprogram is used by BOTH subroutine as well (thanks to &FORBOTH)  */
/*                                                                          */
/*--------------------------------------------------------------------------*/

SUBR SUBR(DLTONLY)

    CHGVAR VAR(&FORBOTH) VALUE(&TRUE)
    CHGVAR VAR(&DELETED) VALUE(&DELETED + 1)
    DLTSPLF FILE(&SPSPLNAME) JOB(&SPJOBNBR/&SPJOBUSER/&SPJOBNAME) SPLNBR(&SPSPLNBR)
    MONMSG MSGID(CPF0000) EXEC(DO)
            CHGVAR VAR(&DELETED) VALUE(&DELETED - 1)
            CHGVAR VAR(&NOTDELETED) VALUE(&NOTDELETED + 1)
            CHGVAR VAR(&ERROR) VALUE(&TRUE)
            CHGVAR VAR(&FORBOTH) VALUE(&FALSE)
            SNDPGMMSG TOPGMQ(*SAME) MSGID(USP0312) MSGDTA(&MSGDTA) MSGF(TOOMSGF) MSGTYPE(*DIAG)
    ENDDO

ENDSUBR

/*--------------------------------------------------------------------------*/
/* ACTION = *BOTH                                                           */
/* We try to delete the spool file                                          */
/* If success                                                               */
/*   - we add one to the number of deleted spool files                      */
/*   - we send USP0302 information message to the joblog                    */
/* If failure                                                               */
/*   - we set the "at least one issue indicator" to yes                     */
/*   - we add one to the number of not deleted spool files                  */
/*                                                                          */
/*--------------------------------------------------------------------------*/

SUBR SUBR(BOTH)

    CALLSUBR SUBR(DLTONLY)
    IF COND(&FORBOTH) THEN(DO)
        CHGVAR VAR(&MSGDTA) VALUE(&SPSPLNAME *CAT &SPSPLNBRA *CAT &SPJOBNBR *CAT &SPJOBUSER *CAT &SPJOBNAME)
        SNDPGMMSG TOPGMQ(*SAME) MSGID(USP0302) MSGDTA(&MSGDTA) MSGF(TOOMSGF) MSGTYPE(*INFO)
    ENDDO

ENDSUBR

ENDPGM