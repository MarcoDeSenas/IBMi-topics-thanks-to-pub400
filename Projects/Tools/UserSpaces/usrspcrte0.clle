
/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Validity checker program for USRSPCRTVE command.                         */
/*                                                                          */
/* Dates: 2025/08/03 Creation                                               */
/*                                                                          */
/*--------------------------------------------------------------------------*/

PGM PARM(&USRSPCQUAL &STARTPOS &ENTRYLG &ERRC0100 &ENTRYDTA)

DCLPRCOPT DFTACTGRP(*NO) ACTGRP(USRSPC)

DCL VAR(&USRSPCQUAL) TYPE(*CHAR) LEN(20)

DCL VAR(&USRSPC) TYPE(*CHAR) LEN(10) STG(*DEFINED) DEFVAR(&USRSPCQUAL 1)
DCL VAR(&USRSPCLIB) TYPE(*CHAR) LEN(10) STG(*DEFINED) DEFVAR(&USRSPCQUAL 11)

DCL VAR(&PARAM1) TYPE(*CHAR) LEN(10) VALUE('USRSPC')

INCLUDE SRCSTMF('../../Common/Includes/inc_variables_declare.clle')
INCLUDE SRCSTMF('../../Common/Includes/inc_stdapi_declare.clle')

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* We cannot include the standard error handling routine because system     */
/* programs which run the validity checker expect only CPF0002 escape       */
/* when they call it.                                                       */
/* So, if an unexpected error occurs we handle it just like if it was an    */
/* error from one of the checks. And a specific error handling routine      */
/* exists for validity checker programs.                                    */
/*                                                                          */
/*--------------------------------------------------------------------------*/

INCLUDE +
    SRCSTMF('../../Common/Includes/inc_errorhandling_forchecker_declare.clle')

MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(ERROR))

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Some initializations...                                                  */
/*                                                                          */
/*--------------------------------------------------------------------------*/

INCLUDE SRCSTMF('../../Common/Includes/inc_variables_init.clle')
CHGVAR VAR(&ERROR) VALUE(&FALSE)
CHGVAR VAR(&ERRORPARAM) VALUE(&FALSE)

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Checkings are the following:                                             */
/*                                                                          */
/* USRSPC parameter must be valid names.                                    */
/* User space library can be *CURLIB, *LIBL                                 */
/*                                                                          */
/*--------------------------------------------------------------------------*/

CHGVAR VAR(&USRSPC) VALUE(%SST(&USRSPCQUAL 1 10))
CHGVAR VAR(&USRSPCLIB) VALUE(%SST(&USRSPCQUAL 11 10))

CHKOBJ OBJ(QTEMP/&USRSPC) OBJTYPE(*USRPRF)
MONMSG MSGID(CPF0000) EXEC(DO)
    RCVMSG MSGTYPE(*EXCP) RMV(*YES)
    CHGVAR VAR(&MSGID) VALUE(&BLANK)
    RCVMSG MSGTYPE(*DIAG) MSGID(&MSGID) RMV(*YES)
    IF COND(&MSGID *EQ 'CPD0078') THEN(DO)
        CHGVAR VAR(&ERRORPARAM) VALUE(&TRUE)
        CHGVAR VAR(&MSGDTA) VALUE('0000')
        CHGVAR VAR(%SST(&MSGDTA 5 10)) VALUE(&PARAM1)
        CHGVAR VAR(%SST(&MSGDTA 15 10)) VALUE(&USRSPC)
        SNDPGMMSG MSGID(CPD0084) MSGF(QCPFMSG) MSGTYPE(*DIAG) MSGDTA(&MSGDTA)
    ENDDO
ENDDO

IF COND(&USRSPCLIB *NE '*CURLIB' *AND &USRSPCLIB *NE '*LIBL') THEN(DO)
    CHKOBJ OBJ(QTEMP/&USRSPCLIB) OBJTYPE(*USRPRF)
    MONMSG MSGID(CPF0000) EXEC(DO)
        RCVMSG MSGTYPE(*EXCP) RMV(*YES)
        CHGVAR VAR(&MSGID) VALUE(&BLANK)
        RCVMSG MSGTYPE(*DIAG) MSGID(&MSGID) RMV(*YES)
        IF COND(&MSGID *EQ 'CPD0078') THEN(DO)
            CHGVAR VAR(&ERRORPARAM) VALUE(&TRUE)
            CHGVAR VAR(&MSGDTA) VALUE('0000')
            CHGVAR VAR(%SST(&MSGDTA 5 10)) VALUE(&PARAM1)
            CHGVAR VAR(%SST(&MSGDTA 15 10)) VALUE(&USRSPCLIB)
            SNDPGMMSG MSGID(CPD0084) MSGF(QCPFMSG) MSGTYPE(*DIAG) MSGDTA(&MSGDTA)
        ENDDO
    ENDDO
ENDDO

/*--------------------------------------------------------------------------*/
/*                                                                          */
/* Error hangling.                                                          */
/*                                                                          */
/*--------------------------------------------------------------------------*/

INCLUDE +
    SRCSTMF('../../Common/Includes/inc_errorhandling_forchecker_routine.clle')

ENDPGM
